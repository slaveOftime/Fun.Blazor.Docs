<pre><code class="language-fsharp">[&lt;AutoOpen&gt;]
module Fun.Blazor.Docs.Wasm.Pages.HelperFunctions.ComponentHookDemo

open System
open FSharp.Control.Reactive
open MudBlazor
open Fun.Result
open Fun.Blazor

let componentHookDemo =
    html.inject (fun (hook: IComponentHook) -&gt;
        let toggle = hook.UseStore false
        let count = hook.UseStore 0
        let threshhold = 3

        hook.OnInitialized.Add(fun () -&gt;
            toggle.Publish true
            count.Publish((+) 10)
        )

        hook.OnFirstAfterRender.Add(fun () -&gt;
            TimeSpan.FromSeconds 1.
            |&gt; Observable.interval
            |&gt; Observable.subscribe (fun _ -&gt; count.Publish(fun x -&gt; x + 1))
            |&gt; hook.AddDispose

            count.Observable
            |&gt; Observable.subscribe (
                function
                | GreaterThan threshhold -&gt;
                    toggle.Publish not
                    count.Publish 0
                | _ -&gt; ()
            )
            |&gt; hook.AddDispose
        )

        MudPaper'() {
            styleBuilder { padding 20 }
            childContent [
                html.watch (
                    toggle,
                    fun isToggled -&gt;
                        [
                            MudText'() {
                                Typo Typo.subtitle1
                                childContent
                                    "We can use this hook to subscribe lifecycle event of the component which created by html.inject at the beginening. We can create observable store to have a state management for the current component. After the component disposed all the store will also be disposed."
                            }
                            MudText'() {
                                Typo Typo.subtitle1
                                Color Color.Info
                                childContent $"After every {threshhold} seconds the toggle will be switched"
                            }
                            MudSwitch'&lt;bool&gt;() {
                                Checked isToggled
                                CheckedChanged(fun (x: bool) -&gt; toggle.Publish x)
                            }
                            if isToggled then
                                MudText'() {
                                    Typo Typo.subtitle2
                                    Color Color.Primary
                                    childContent "Toggled successfuly"
                                }
                            else
                                MudText'() {
                                    Color Color.Secondary
                                    childContent "Toggled off now"
                                }
                            html.watch (count, (fun c -&gt; html.text $"Interval Count: {c}"))
                        ]
                )
            ]
        }
    )

</code></pre>
