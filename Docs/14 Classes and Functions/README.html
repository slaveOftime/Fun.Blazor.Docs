<h1 id="classes-vs-functions">Classes vs Functions</h1>
<p>Fun.Blazor is very flexible and allows you to choose between classes and functions as you need.</p>
<p>For the most part you can use functions as much as you'd like but there are situations wher classes may come in handy (like writing blazor components for other teams).</p>
<h2 id="functions">Functions</h2>
<p>Functions are executed once and they are not re-executed unless the parent re-renders</p>
<pre><code class="language-fsharp">let staticGreeting name =
    p { $&quot;Hello there, {name}!&quot; }
</code></pre>
<p>This function given the same parameter will always output the same p tag with the same content and it will never change again, the same goes for functions that enclose mutable values, for example the following function will not re-render</p>
<pre><code class="language-fsharp">let staticGreeting()=
    let mutable counter = 0

    button {
        on.click (fun _ -&gt; counter &lt;- counter + 1)
        $&quot;Click Me: {counter}&quot;
    }
</code></pre>
<p>This is because the change detection mechanics happen at the component level, that means class inheriting ComponentBase which in the case of Fun.Blazor is <code>FunComponent</code>.
However If change the declaration from a function (<code>let staticGreeting () =</code>) to a let bound value (<code>let staticGreeting =</code>) , and use it in a context which its parent component has re-render triggers (like event handlers in <code>FunComponent</code>s, see the below section on classes.), the count will increase and updated.</p>
<p>However if you want to work with dynamic data in your functions, you can use <code>Adaptive</code> values, <code>Stores</code>, or <code>Observables</code> which you can read further in <a href="Docs/./Interactive-Nodes">Interactive Nodes</a>. A brief example with adaptive values would be:</p>
<pre><code class="language-fsharp">
let staticChild name (currentName: string cval) =
    button {
        // trigger currentName changes
        on.click(fun _ -&gt; currentname.Publish name)
        $&quot;Click {name}!&quot;
    }

let staticParent () =
    let currentName = cval(&quot;Nobody&quot;)
    fragment {
        // doesn't re-render
        h1 { $&quot;Initial Name: {currentName.Value}&quot; }

        // render on currentName changes
        adaptiview() {
            let! currentName = currentName
            h1 { $&quot;Hello, {currentName}!&quot; }
        }

        staticChild &quot;Peter&quot; currentName
        staticChild &quot;Frank&quot; currentName
    }
</code></pre>
<p>This reduces the need to have classes for most of the codebase, you can combine dynamic data with Fun.Blazor's DSL to go as functional as you need.</p>
<h3 id="benefits-and-drawbacks">Benefits and drawbacks</h3>
<p>The benefits are:</p>
<ul>
<li>Simpler to test</li>
<li>Easier to reason about</li>
<li>Composable</li>
</ul>
<p>They are often the preferred way to go for most F# users.</p>
<p>The downsides are:</p>
<ul>
<li>Don't have access to aspnet's default DI container</li>
<li>Don't play well raw blazor</li>
<li>They need to be wrapped in components in order for C# consumers to use</li>
</ul>
<p>In short the closer you want to get to Blazor, the least functions work nicely with it.</p>
<p>However, most of the time a function is the simple yet correct choice.</p>
<h2 id="classes">Classes</h2>
<p>Blazor by default re-renders in certain conditions like after an event handler is executed, changing parameters/cascading parameters and when calling <code>this.StateHasChanged()</code> so it plays well with mutable values even in F#.</p>
<p>A simple counter example would be the following:</p>
<pre><code class="language-fsharp">type Counter() =
    inherit FunComponent()

    let mutable counter = 0

    [&lt;Inject&gt;]
    member val Logger: ILogger&lt;Counter&gt; = Unchecked.defaultof&lt;_&gt; with get, set

    override this.Render() =
        fragment {
            h1 { $&quot;Count: {counter}&quot; }

            button {
                on.click (fun _ -&gt;
                    let newCount = counter + 1
                    this.Logger.LogDebug(&quot;Old Value: {count} - New Value: {newCount}&quot;, counter, newCount)
                    counter &lt;- newCount
                )
                &quot;Increment&quot;
            }
        }
</code></pre>
<p>In this case we didn't add any dynamic values like Adaptive data, Stores or Observables and our counter is re-rendered and works as expected, blazor applies a diffing algorithm to know that it only needs to re-render the <code>h1</code> tag. you can also however use dynamic data if required.</p>
<h3 id="using-adaptive-data">Using adaptive data</h3>
<p>If you would like to avoid re-rendering your components at event handlers, you can opt out of it by using <code>FunBlazorComponent</code> this base class turns off re-renders for event triggers.</p>
<pre><code class="language-fsharp">type Demo() =
    // Note this is not the same as *inherit FunComponent()*
    inherit FunBlazorComponent()

    let data = cval 0

    override _.Render() = fragment {
        h1 { &quot;Static text&quot; }
        adaptiview() {
            let! data = data
            // only this part will need to be used for computate for diff when data is changed
            p { $&quot;x = {data}&quot; }
        }
        button {
            on.click (fun _ -&gt; data.Publish(fun value -&gt; value + 1))
            &quot;Increase&quot;
        }
    }
</code></pre>
<p>This has the added benefit that updates are more localized and you don't accidentally re-render on event handlers, most of the time it should not be an issue, but if you require to have more control of it, then this is the ideal way to do it.</p>
<h3 id="benefits-and-drawbacks-1">Benefits and drawbacks</h3>
<p>In general classes are good to use within Fun.Blazor and are a safe choice if your project needs them.</p>
<p>The benefits are:</p>
<ul>
<li>Offer full decorator support <code>Parameter</code>, <code>Cascading Parameter</code>, <code>Route</code>, <code>Inject</code></li>
<li>Has direct access to the DI container in aspnet</li>
<li>Consumption is transparent for C# consumers</li>
<li>Plays well with the blazor framework under the hood.</li>
</ul>
<p>The drawbacks are:</p>
<ul>
<li>They require manual bindings to play well with the Fun.Blazor DSL</li>
<li>Verbose</li>
</ul>
<p>In general, classes can be used to orchestrate dependency injection, to use plain blazor features seamlessly, and to keep as close as blazor as possible.</p>
<p>Functions most of the time can overcome the benefits of classes when you use <code>html.inject</code> which you can read more about in <a href="Docs/./Advanced-features/Hook">Hooks</a> and <a href="Docs/./Advanced-features/Dependency-injection-(DI)">Dependency Injection</a>.</p>
