<pre><code class="language-fsharp">open FSharp.Data.Adaptive
open MudBlazor // We are using this library for our doc site
open Fun.Blazor
open Fun.Blazor.Validators
open Fun.Blazor.Docs.Controls


type Model =
    {
        Name: string
        Password: string
        Age: int
        Birthday: DateTime
        Address: Address
    }

    static member DefaultValue = {
        Name = &quot;&quot;
        Password = &quot;&quot;
        Age = 0
        Birthday = DateTime.Now
        Address = { Zip = &quot;&quot;; Street = &quot;&quot; }
    }

and Address = { Zip: string; Street: string }

type ModelErrors =
    | NameIsTooShort of int
    | NameIsTooLong of int
    | PasswordIsRequired
    | PasswordCannotEqualName
    | PasswordIsTooLong of int
    | PasswordCannotBe of string
    | AgeIsTooSmall of int
    | AgeCannotEqual of int
    | BirthdayIsTooEarly of DateTime
    | BirthdayIsTooOld of DateTime
    | AddressError of AddressError

and AddressError = | ZipCodeCannotBeEmpty


[&lt;AutoOpen&gt;]
module Extensions =
    open Fun.Blazor.Operators
    open MudBlazor.DslInternals

    type MudFormComponentBuilder&lt;'FunBlazorGeneric, 'T, 'U when 'FunBlazorGeneric :&gt; Microsoft.AspNetCore.Components.IComponent&gt; with

        [&lt;CustomOperation &quot;Errors&quot;&gt;]
        member inline _.Errors([&lt;InlineIfLambda&gt;] render: AttrRenderFragment, errors: _ seq) =
            render
            ==&gt; MudFormComponent'() {
                Error(errors |&gt; Seq.isEmpty |&gt; not)
                ErrorText(errors |&gt; Seq.map string |&gt; String.concat &quot;, &quot;)
                asAttrRenderFragment
            }


// This is used to demo nest/sub form
let private addressForm (modelForm: AdaptiveForm&lt;Address, AddressError&gt;) =
    html.fragment [|
        adaptiview () {
            let! binding, errors = modelForm.UseFieldWithErrors(fun x -&gt; x.Zip)
            MudTextField'() {
                Label &quot;Zip code&quot;
                Value' binding
                Immediate true
                Errors errors
            }
        }
        adaptiview () {
            let! binding = modelForm.UseField(fun x -&gt; x.Street)
            MudTextField'() {
                Label &quot;Street&quot;
                Value' binding
                Immediate true
            }
        }
    |]


let entry =
    html.inject (fun (hook: IComponentHook) -&gt;
        let modelForm =
            hook
                .UseAdaptiveForm&lt;Model, ModelErrors&gt;(Model.DefaultValue)
                .AddValidators(
                    (fun x -&gt; x.Name),
                    false,
                    [
                        required (NameIsTooShort 0)
                        minLength 2 NameIsTooShort
                        maxLength 10 NameIsTooLong
                    ]
                )
                .AddValidators(
                    (fun x -&gt; x.Password),
                    true,
                    [
                        required PasswordIsRequired
                        maxLength 15 PasswordIsTooLong
                        notEqual &quot;123456&quot; PasswordCannotBe
                        fun ctx value -&gt;
                            let name = ctx.GetFieldValue(fun x -&gt; x.Name)
                            if name = value then [ PasswordCannotEqualName ] else []
                    ]
                )
                .AddValidators((fun x -&gt; x.Age), false, [ minValue 18 AgeIsTooSmall ])
                .AddValidators(
                    (fun x -&gt; x.Birthday),
                    true,
                    [
                        minValue (DateTime.Parse(&quot;2000/1/1&quot;)) BirthdayIsTooOld
                        maxValue (DateTime.Now.AddDays(1.)) BirthdayIsTooEarly
                    ]
                )

        let addressModelForm =
            modelForm
                .GetSubForm((fun x -&gt; x.Address), AddressError)
                .AddValidators((fun x -&gt; x.Zip), true, [ required ZipCodeCannotBeEmpty ])

        MudPaper'() {
            Elevation 10
            style { padding 10 }
            childContent [
                MudForm'.create [
                    adaptiview () {
                        let! binding, errors = modelForm.UseFieldWithErrors(fun x -&gt; x.Name)
                        MudTextField'() {
                            Label &quot;Name&quot;
                            Value' binding
                            Immediate true
                            Required true
                            Errors errors
                        }
                    }
                    adaptiview () {
                        let! binding, errors = modelForm.UseFieldWithErrors(fun x -&gt; x.Password)
                        MudTextField'() {
                            Label &quot;Password&quot;
                            Value' binding
                            Immediate true
                            InputType InputType.Password
                            Required true
                            Errors errors
                        }
                    }
                    adaptiview () {
                        let! binding, errors = modelForm.UseFieldWithErrors(fun x -&gt; x.Age)
                        MudTextField'() {
                            Label &quot;Age&quot;
                            Value' binding
                            Immediate true
                            InputType InputType.Number
                            Errors errors
                        }
                    }
                    adaptiview () {
                        let! (value', setValue), errors = modelForm.UseFieldWithErrors(fun x -&gt; x.Birthday)
                        MudDatePicker'() {
                            Label &quot;Birthday&quot;
                            Date(Nullable value')
                            DateChanged(Option.ofNullable &gt;&gt; Option.iter setValue)
                            Errors errors
                        }
                    }
                    addressForm addressModelForm
                ]
                spaceV4
                adaptiview () {
                    let! errors = modelForm.UseErrors()
                    MudAlert'() {
                        Severity Severity.Info
                        $&quot;Total errors is {errors.Length}&quot;
                    }
                }
                spaceV4
                adaptiview () {
                    let! hasChanges = modelForm.UseHasChanges()
                    if hasChanges then
                        MudAlert'() {
                            Severity Severity.Info
                            &quot;There are some changes&quot;
                        }
                    MudButton'() {
                        OnClick(fun _ -&gt; modelForm.SetValue(Model.DefaultValue))
                        &quot;Reset&quot;
                    }
                    MudButton'() {
                        OnClick(fun _ -&gt; modelForm.UseFieldSetter (fun x -&gt; x.Age) (24))
                        &quot;Set age to 24&quot;
                    }
                }
            ]
        }
    )
</code></pre>
